# === ДЕНЬ 2: «ГОРОД» ===
label day2_sleep:
    scene black with fade
    n_narr "Сон был коротким и тревожным. Неудивительно, после вчерашних то событий"
    n_narr "Но утро пришло без солнца. Только дым и гул артиллерии на горизонте."
    n_narr "Мы сейчас отправляемся в сторону Бобруйска, чтобы помочь в зачистке города."

    nvl hide
    nvl clear

    jump day2_start

label day2_start:
    scene city_ruins_morning with fade
    play music theme_tense fadein 2.0
    play sound distant_gunfire loop

    n_narr "К полудню мы добрались до места назначения."
    n_narr "Сегодня 30 июня. Мы стоим на окраине почти освобождённого города - Бобруйска. Вчера здесь бушевал ад."
    n_narr "Теперь — мёртвая тишина, нарушаемая лишь треском горящих балок и стонами раненых. Воздух пропитан запахом гари, пыли и крови."
    n_narr "Вчера, к десяти часам утра, город был наш. Но победа далась страшной ценой."
    n_narr "Говорят, под Бобруйском фашисты потеряли пятьдесят тысяч своих. «Котёл» ликвидирован."

    nvl hide
    nvl clear

    ivan "Внимание! Враг отступил, но оставил «крыс» — снайперов и миномётчиков. Двигаемся осторожно, по укрытиям!"
    politruk "Помните, товарищи! Этот город — наша земля. Каждый дом, каждая улица — свидетель зверств оккупантов. Наша задача зачистить его до конца!"

    #ДОБАВЬ МУЗЫКУ ТИПО ВОЕННЫЕ ПРЕСТУПЛЕНИЯ
    n_narr "Мы продвигаемся вглубь. Нас встречают обугленные, обстрелянные дома. На дороге — следы крови и сгоревшая техника."
    n_narr "Проходим мимо разрушенного здания. На асфальте неподалеку виднеется надпись мелом: «ДЕТИ». Сердце сжимается."

    nvl hide
    nvl clear

    scene city_courtyard with dissolve
    civil "Кто-нибудь, помогите!"

    n_narr "Из подвала одного из домов выбегает мужчина лет сорока. Его лицо покрыто сажей, руки в ожогах и ссадинах, глаза полны отчаяния."

    nvl hide
    nvl clear

    civil "Мой брат… он ранен! Вчера, когда начался штурм, его ранило и теперь он не может идти!"
    ivan "Где он? Проведи наc к нему."
    civil "Там, в подвале соседнего дома. Но… там ещё и фриц... Раненый... Они вместе, как в ловушке."

    n_narr "Старшина Иван переглядывается со мной. Ситуация сложная. Раненый враг — это опасность и отвестенность за соотечественника."

    nvl hide
    nvl clear

    menu choice_wounded_enemy:
        "Идти спасать обоих":
            narr "Раненый — не враг. Он уже не опасен."
            n_narr "Мы осторожно спускаемся в полутёмный подвал. На полу лежат двое: наш парень и немец."
            n_narr "Немец смотрит на нас с ненавистью, но силы держать винтовку у него нет. Наш раненый стонет от боли."
            n_narr "И тут немец на ломаном русском с сильным немецким акцентом говорит:"
            hanz "Те деди, которых мы оставить в живых.. Они все бутуд должны служить и зигавать нашему фюрреру! "
            #ABSOLUTE CINEMA
            n_narr "Не обращая внимания на его возгласы, мы продалжаем операцию по спасению."            
            n_narr "Я бросаю свой бинт местному жителю: «Перевяжи его. Быстро!»"
            n_narr "Иван приказывает: «Забираем своего. Немца оставляем. Пусть медсанбат разбирается»."
            $ showed_mercy = True

        "Забрать только нашего":
            narr "Наша задача — сохранить своих. Остальное — не наше дело."
            n_narr "Мы забираем гражданского. Местный житель помогает нам вынести его на улицу."
            n_narr "В подвале остаётся стон раненого немца. Коля смотрит вниз с ужасом, но молчит."
            n_narr "Иван коротко командует: «Закройте вход. Пусть сидит там, пока не подохнет или пока его не найдут»."
            $ showed_mercy = False

    nvl hide
    nvl clear

    scene city_street_ruins with dissolve
    play sound machine_gun
    scene city_street_ruins with hpunch

    n_narr "Продолжаем движение. Внезапная очередь заставила нас всех спрятаться по углам. Пули свистят над головами."

    n_narr "Огонь ведётся из здания бывшего почтамта. Пулемёт простреливает всю улицу. Продвижение невозможно."

    nvl hide
    nvl clear

    masha "Я знаю это здание! Слева — разрушенная лестница на второй этаж. Справа — подвал, выходящий во двор."
    masha "Могу провести обход с тыла. Тихо. Но нужно удостовериться в безопасности."

    n_narr "Старшина Иван смотрит на меня. Время решать."

    nvl hide
    nvl clear

    menu choice_post_office:
        "Бросить гранату в окно":
            narr "Нет времени на обходы. Нужно подавить огневую точку сейчас."
            play sound grenade_throw
            n_narr "Я вырываю гранату из подсумка и метаю её в разбитое окно второго этажа."
            play sound explosion_loud
            scene city_street_ruins with vpunch
            n_narr "Взрыв. Облако пыли и дыма. Огонь прекратился."
            n_narr "Мы врываемся внутрь… В подвале, под завалами, слышен детский плач."

            jump day2_basement

        "Пойти на обход с Машей":
            narr "Лучше не рисковать. Вдруг там гражданские…"
            masha "За мной. И тише воды, ниже травы."
            n_narr "Мы пробираемся по завалам во двор, затем — в подвал здания."
            n_narr "Из темноты на нас смотрят две пары испуганных глаз. Старуха прижимает к себе ребёнка."
            n_narr "Рядом с ними — деревянный ящик с красным крестом."
            $ masha_rep += 1

            jump day2_basement_peaceful

label day2_basement_peaceful:
    scene city_basement with fade
    stop sound
    play sound child_crying

    n_narr "Подвал наполнен сыростью и страхом. На полу, прижавшись к углу, сидит старуха с ребенком на руках."
    n_narr "Ящик с медикаментами стоит в углу. На нём — следы немецких сапог. Видимо, его готовили к эвакуации."

    nvl hide
    nvl clear
    jump choice_med

label day2_basement:
    scene city_basement with fade
    stop sound
    play sound child_crying

    n_narr "Подвал наполнен сыростью и страхом. Старуха, чудом оставшаяся в живых, дрожит, прикрывая ребёнка своим телом."
    n_narr "Ящик с медикаментами стоит в углу. На нём — следы немецких сапог. Видимо, его готовили к эвакуации."

    nvl hide
    nvl clear
    jump choice_med

label choice_med:
    menu choice_medicine:
        "Что сделать с ящиком?"
        "Забрать всё содержимое":
            narr "Война не щадит никого. Наши раненые — в первую очередь."
            n_narr "Я беру ящик. Старуха тихо всхлипывает, но не сопротивляется."
            n_narr "Маша отводит взгляд. Коля сжимает губы."

        "Взять только бинты и перевязочные пакеты":
            narr "Они тоже люди. Оставим им хоть что-то."
            n_narr "Я вынимаю из ящика только самое необходимое для полевого госпиталя — бинты и пакеты."
            n_narr "Старуха кивает, её глаза полны слёз благодарности."
            n_narr "Маша кладёт руку мне на плечо и шепчет:"
            masha "Спасибо"
            $ masha_rep +=1

    stop sound
    nvl hide
    nvl clear

    scene destroyed_house with dissolve
    n_narr "Пока отряд отдыхает, я замечаю полуразрушенный дом с выгоревшим фасадом. На обугленной двери — шестиконечная звезда, вырезанная топором."
    n_narr "Маша, увидев это, замирает. Её лицо становится каменным."

    nvl hide
    nvl clear

    play music warum loop
    masha "Это было гетто… Здесь жили евреи."
    scene ghetto with dissolve
    masha "Осенью сорок первого… их всех согнали сюда. А потом… потом большую часть повели в лес, за город."
    masha "Говорят, крики были слышны на другом берегу Березины. А потом — тишина. Их больше не было."

    n_narr "Она не плачет. Её боль глубже слёз. Она — память о тех, кого уже не вернуть."
    n_narr "Я кладу руку на обгоревшую дверь. Три года оккупации. Тысячи убитых мирных жителей. Сотни расстрелянных семей." 

    nvl hide
    nvl clear

    scene city_square_evening with fade
    stop music fadeout 2.0
    stop sound fadeout 2.0
    play music theme_calm fadein 2.0
    play sound distant_celebration

    n_narr "К вечеру город полностью в наших руках. Остатки немецкого гарнизона уничтожены или взяты в плен."
    n_narr "Жители, прятавшиеся в подвалах, выходят на улицы. Их лица — смесь радости, боли и неверия."

    nvl hide
    nvl clear

    ivan "Молодцы, ребята! Но расслабляться рано. Получен приказ: преследовать отступающего врага. Направление — Минск."
    politruk "Минск ждёт нас! Путь к столице открыт!"

    n_narr "Мы смотрим на руины. Сегодня мы освободили город. Завтра — снова в бой."
    n_narr "Нужно помнить. Помнить о Бобруйске. Помнить о всех городах и деревнях Беларуси. Об их мёртвых улицах и живой боли его жителей."

    stop sound
    scene black with fade
    n_narr "День второй позади. Впереди — леса, болота и погоня за отступающим врагом."

    jump day34_start